***( IPDL

  Copyright : (C) 2022-23, Kristina Sojakova, Mihai Codescu
  License   : GNU General Public License v3.0 or later (GPL-3.0-or-later)
  Authors   : Mihai Codescu <mscodescu@gmail.com>
              Kristina Sojakova <sojakova.kristina@gmail.com>

Symmetric-Key Encryption: CPA Security
Section 1 of doc/case_studies.pdf

)
load ../src/strategies

mod CPA-SECURITY is
 protecting APPROX-EQUALITY .
 
 op q : -> Nat .
 
 op msg  : -> Type .
 op key  : -> Type .
 op ctxt : -> Type .
 
 op zerosF : -> SigElem .
 eq zerosF = 'zeros : unit ~> msg .
 
 op genKeyF : -> SigElem .
 eq genKeyF = 'genKey : unit ~>> key .
 
 op encF : -> SigElem .
 eq encF = 'enc : (msg * key) ~>> ctxt .
 
 op sig : -> Signature .
 eq sig = encF genKeyF zerosF .
 
 op lrLeft : -> Protocol .
 eq lrLeft = 
  new 'Key : key in
   (
    ('Key ::= nf(emptyBRList, samp ('genKey < () >), nil))
    ||
    (family ('Enc[bound q]) 'i (bound q) ::= 
      nf( ('ml : msg <- read ('Msg-L['i]))
          ('mr : msg <- read ('Msg-R['i]))
          ('k : key <- read 'Key),
          samp ('enc < pair('ml, 'k) >),
           'ml 'mr 'k)
    ) 
   ) 
 .
 
 op lrRight : -> Protocol .
 eq lrRight = 
  new 'Key : key in
   (
    ('Key ::= nf(emptyBRList, samp ('genKey < () >), nil))
    ||
    (family ('Enc[bound q]) 'i (bound q) ::= 
      nf( ('ml : msg <- read ('Msg-L['i]))
          ('mr : msg <- read ('Msg-R['i]))
          ('k : key <- read 'Key),
          samp ('enc < pair('mr, 'k) >),
           'ml 'mr 'k)
    ) 
   ) 
 .
 
 op cmLeft : -> Protocol .
 eq cmLeft = 
  new 'Key : key in
   (
    ('Key ::= nf(emptyBRList, samp ('genKey < () >), nil))
    ||
    (family ('Enc[bound q]) 'i (bound q) ::= 
      nf( ('m : msg <- read ('Msg['i]))
          ('k : key <- read 'Key),
          samp ('enc < pair('m, 'k) >),
           'm 'k)
    ) 
   ) 
 .

 op cmRight : -> Protocol .
 eq cmRight = 
  new 'Key : key in
   (
    ('Key ::= nf(emptyBRList, samp ('genKey < () >), nil))
    ||
    (family ('Enc[bound q]) 'i (bound q) ::= 
      nf( ('m : msg <- read ('Msg['i]))
          ('k : key <- read 'Key),
          samp ('enc < pair(ap 'zeros (), 'k) >),
           'm 'k )
    ) 
   ) 
 .
 
 *** intermediary protocol in first proof
 
 op int0 : -> Protocol .
 eq int0 = newfamily 'Msg-L[bound q] 'i (bound q) : msg in 
           newfamily 'Msg-R[bound q] 'i (bound q) : msg in
    (
    lrLeft ||
    (family ('Msg-L[bound q]) 'i (bound q) ::= 
      nf(
      'm : msg <- read ('Msg['i]),
      return 'm,
      'm
      )
    )
    ||
    (family ('Msg-R[bound q]) 'i (bound q) ::= 
      nf(emptyBRList, return (ap 'zeros ()), nil)
    )
    )
 .
    
  *** first intermediary protocol in second proof
 
 op int1 : -> Protocol .
 eq int1 = 
     newfamily ('Msg[bound q]) 'i (bound q) : msg in 
     (
     cmLeft ||
     (family ('Msg[bound q]) 'i (bound q) ::= 
      nf(
      ('ml : msg <- read ('Msg-L['i]))
      ('mr : msg <- read ('Msg-R['i])),
      return 'ml,
      'ml 'mr
      )
    )
     )
 . 
 
 *** second intermediary protocol in second proof
 
 op int2 : -> Protocol .
 eq int2 = 
     newfamily ('Msg[bound q]) 'i (bound q) : msg in 
     (
     cmRight ||
     (family ('Msg[bound q]) 'i (bound q) ::= 
      nf(
      ('ml : msg <- read ('Msg-L['i]))
      ('mr : msg <- read ('Msg-R['i])),
      return 'mr,
      'ml 'mr
      )
    )
     )
 . 
 
  *** third intermediary protocol in second proof
 
 op int3 : -> Protocol .
 eq int3 = 
     newfamily ('Msg[bound q]) 'i (bound q) : msg in 
     (
     cmLeft ||
     (family ('Msg[bound q]) 'i (bound q) ::= 
      nf(
      ('ml : msg <- read ('Msg-L['i]))
      ('mr : msg <- read ('Msg-R['i])),
      return 'mr,
      'ml 'mr
      )
    )
     )
 .
    
 
endm 

smod EXECUTE is
 pr STRATS .
 pr CPA-SECURITY .
 
 var i m ml mr k l ka kb C sk : Qid .
 var Key Enc Msg Msg-L Msg-R : Qid .
 var Sigma Sigma1 : Signature .
 var Delta Delta' Delta1 : ChannelContext .
 var Gamma : TypeContext .
 var I O I1 O1 : Set{CNameBound} .
 var li : List{NatTerm} .
 var A A1 : Set{BoolTerm} .
 var wid len : Nat . 
 var pConf pConf' : ProtocolConfig .
 var aConf : ApproxEqConfig .
 var P P1 : Protocol .

 crl [cm] : 
    aConfig(Sigma, Delta ,
            newNF( < Key : key >, 
   (
    (Key ::= nf(emptyBRList, samp ('genKey < () >), nil))
    ||
    (family (Enc[bound q]) i (bound q) ::= 
      nf( (m : msg <- read (Msg[i]))
          (k : key <- read Key),
          samp ('enc < pair(m, k) >),
          m k)
    ) 
   ), chn Key) , I, O, A, width wid, length len
   )
   =>
   aConfig(Sigma, Delta ,
            newNF( < Key : key >, 
   (
    (Key ::= nf(emptyBRList, samp ('genKey < () >), nil))
    ||
    (family (Enc[bound q]) i (bound q) ::= 
      nf( (m : msg <- read (Msg[i]))
          (k : key <- read Key),
          samp ('enc < pair(ap 'zeros (), k) >),
          m k)
    ) 
   ), chn Key) , I, O, A, width (wid + 1), length (diff(len + | I |, 1))
   )
   
   if
   
    (fam (Msg[bound q])) in I 
    
    /\
    
    O == (fam (Enc[bound q]))
    
   .
   
   strat applyCM @ ProtocolConfig .
   sd applyCM :=
    match aConf s.t. aConfStartsWithNew aConf
    ? CONG-NEW-NF-APPROX{applyCM}
    : CONG-COMP-APPROX{cm}
     .     
   
   crl [lr] : 
    aConfig(Sigma, Delta ,
            newNF( < Key : key >, 
   (
    (Key ::= nf(emptyBRList, samp ('genKey < () >), nil))
    ||
    (family (Enc[bound q]) i (bound q) ::= 
      nf( (ml : msg <- read (Msg-L[ i ]))
          (mr : msg <- read (Msg-R[ i ]))
          (k : key <- read Key),
          samp ('enc < pair(ml, k) >),
          ml mr k)
    ) 
   ) , chn Key)  , I, O, A, width wid, length len
   )
   =>
   aConfig(Sigma, Delta ,
            newNF( < Key : key > , 
   (
    (Key ::= nf(emptyBRList, samp ('genKey < () >), nil))
    ||
    (family (Enc[bound q]) i (bound q) ::= 
      nf( (ml : msg <- read (Msg-L[ i ]))
          (mr : msg <- read (Msg-R[ i ]))
          (k : key <- read Key),
          samp ('enc < pair(mr, k) >),
          ml mr k)
    ) 
   ), chn Key) , I, O, A, width (wid + 1), length (diff(len + | I |, 2))
   )
   if
   
    (fam (Msg-L[bound q])) in I 
    
    /\
    
    (fam (Msg-R[bound q])) in I 
    
    /\
    
    O == fam (Enc[bound q])
   .
   
   strat applyLR @ ProtocolConfig .
   sd applyLR :=
    match aConf s.t. aConfStartsWithNew aConf
    ? CONG-NEW-NF-APPROX{applyLR}
    : CONG-COMP-APPROX{lr}
     .     
   
   *** this will get generated from the proof language
   strat applySym1 @ ProtocolConfig .
   sd applySym1 :=
      matchrew pConf s.t. pConfig(Sigma, Delta, P, I, O, A) := pConf by pConf 
       using 
       SYM[
       Delta1:ChannelContext <- Delta,
       O1:Set{CNameBound} <- O,
       P1:Protocol <- 
       addInternalFamily 'Msg-L
       typed msg 
       params 'i (bound q) 
       assigned nf('m : msg <- read ('Msg['i]),
                   return 'm,
                   'm
                  ) 
       in
  (addInternalFamily 'Msg-R
       typed msg 
       params 'i (bound q) 
       assigned nf(emptyBRList, return (ap 'zeros ()), nil) 
       in 
       (change (fam ('Enc[bound q]))
        with nf(('k : key <- read 'Key) 
                ('ml : msg <- read ('Msg-L['i])) 
                 'mr : msg <- read ('Msg-R['i]), 
                 samp ('enc < pair('ml, 'k) >), 
                 'ml 'mr 'k)
        in P)
    )]{
       foldNFFamily(fam ('Msg-L[bound q]), fam ('Enc[bound q]))
     ; foldNFFamily(fam ('Msg-R[bound q]), fam ('Enc[bound q]))
    }
   .
   
endsm

srew [1]
 aConfig(sig,  
          (fam ('Msg[bound q]) :: msg) 
          (fam ('Enc[bound q]) :: ctxt), cmLeft, insert(fam ('Msg[bound q]), empty),
          getOutputs(cmLeft), empty, width 0, length 0)
using TRANS{
      STRICT{
        sugar-newNF
      ; matchrew pConf s.t. pConfig(Sigma, Delta, P, I, O, A) := pConf by pConf 
       using 
       SYM[
       Delta1:ChannelContext <- Delta,
       O1:Set{CNameBound} <- O,
       P1:Protocol <- int0]{
       sugar-newNF
     ; moveNewToFront
     ; foldNFFamily(fam ('Msg-L[bound q]), fam ('Enc[bound q]))
     ; foldNFFamily(fam ('Msg-R[bound q]), fam ('Enc[bound q]))
      }
         
      },
      TRANS{
      TRANS{
      STRICT{
         sugar-newNF
      }
      ,
      applyLR
      },
      STRICT{
       moveNewToFront
     ; foldNFFamily(fam ('Msg-L[bound q]), fam ('Enc[bound q]))
     ; foldNFFamily(fam ('Msg-R[bound q]), fam ('Enc[bound q]))
     ; matchrew pConf s.t. pConfig(Sigma, Delta, P, I, O, A) := pConf by pConf 
       using 
       SYM[
       Delta1:ChannelContext <- Delta,
       O1:Set{CNameBound} <- O,
       P1:Protocol <- cmRight]{sugar-newNF}
      }
      }
      }
.  

srew [1]
 aConfig(sig, (fam ('Msg-L[bound q]) :: msg) 
              (fam ('Msg-R[bound q]) :: msg)
              (fam ('Enc[bound q]) :: ctxt), 
              lrLeft, 
              insert(fam ('Msg-L[bound q]) , fam ('Msg-R[bound q])), 
              getOutputs(lrLeft), empty, width 0, length 0)
using 
   TRANS{
   TRANS{   
      TRANS{
      TRANS{
      STRICT{
       sugar-newNF
      ; matchrew pConf s.t. pConfig(Sigma, Delta, P, I, O, A) := pConf by pConf 
       using 
       SYM[
       Delta1:ChannelContext <- Delta,
       O1:Set{CNameBound} <- O,
       P1:Protocol <- int1]{
         sugar-newNF
     ; moveNewToFront
     ; foldNFFamily(fam ('Msg[bound q]), fam ('Enc[bound q]))
       }
     ; sugar-newNF   
      },
      applyCM
      },
      STRICT{
        moveNewToFront
      ; foldNFFamily(fam ('Msg[bound q]), fam ('Enc[bound q]))
      ; matchrew pConf s.t. pConfig(Sigma, Delta, P, I, O, A) := pConf by pConf 
       using 
       SYM[
       Delta1:ChannelContext <- Delta,
       O1:Set{CNameBound} <- O,
       P1:Protocol <- int2]{
         sugar-newNF
     ; moveNewToFront
     ; foldNFFamily(fam ('Msg[bound q]), fam ('Enc[bound q]))
      }
      ; sugar-newNF
      }  
      },
      matchrew aConf s.t. aConfig(Sigma, Delta, P, I, O, A, 
                                  width wid, length len) := aConf by aConf 
       using 
      SYM-APPROX[Q:Protocol <- int3,
      Delta':ChannelContext <- Delta,
      O':Set{CNameBound} <- O]{
        TRANS{
          STRICT{sugar-newNF},
          applyCM
        }
      }
   },
   STRICT{ 
       sugar-newNF
     ; moveNewToFront
     ; foldNFFamily(fam ('Msg[bound q]), fam ('Enc[bound q]))
     ; matchrew pConf s.t. pConfig(Sigma, Delta, P, I, O, A) := pConf by pConf 
       using 
       SYM[
       Delta1:ChannelContext <- Delta,
       O1:Set{CNameBound} <- O,
       P1:Protocol <- lrRight]{sugar-newNF}
   } 
   }    
.
 
     
***(
srew [1]
 aConfig(sig, (fam ('Msg-L[bound q]) :: msg) 
              (fam ('Msg-R[bound q]) :: msg)
              (fam ('Enc[bound q]) :: ctxt), 
              lrLeft, 
              insert(fam ('Msg-L[bound q]) , fam ('Msg-R[bound q])), 
              getOutputs(lrLeft), empty, width 0, length 0)
using   TRANS{
      TRANS{
      TRANS{
      STRICT{
       sugar-newNF
      ; matchrew pConf s.t. pConfig(Sigma, Delta, P, I, O, A) := pConf by pConf 
       using 
       SYM[
       Delta1:ChannelContext <- Delta,
       O1:Set{CNameBound} <- O,
       P1:Protocol <- int1]{
         sugar-newNF
     ; moveNewToFront
     ; foldNFFamily(fam ('Msg[bound q]), fam ('Enc[bound q]))
       }
     ; sugar-newNF   
      },
      applyCM
      },
      STRICT{
        moveNewToFront
      ; foldNFFamily(fam ('Msg[bound q]), fam ('Enc[bound q]))
      ; matchrew pConf s.t. pConfig(Sigma, Delta, P, I, O, A) := pConf by pConf 
       using 
       SYM[
       Delta1:ChannelContext <- Delta,
       O1:Set{CNameBound} <- O,
       P1:Protocol <- int2]{
         sugar-newNF
     ; moveNewToFront
     ; foldNFFamily(fam ('Msg[bound q]), fam ('Enc[bound q]))
      }
      ; sugar-newNF
      }  
      }
      , 
      matchrew aConf s.t. aConfig(Sigma, Delta, P, I, O, A, 
                                  width wid, length len) := aConf by aConf 
       using 
      SYM-APPROX[Q:Protocol <- P,
      Delta':ChannelContext <- Delta,
      O':Set{CNameBound} <- O
      ]{
        idle
      }
      }

.

set trace off .

srew [1]
 aConfig(sig, (fam ('Msg-L[bound q]) :: msg) 
              (fam ('Msg-R[bound q]) :: msg)
              (fam ('Enc[bound q]) :: ctxt), 
              int3, 
              insert(fam ('Msg-L[bound q]) , fam ('Msg-R[bound q])), 
              getOutputs(int3), empty, width 0, length 0)
using              
      STRICT{
        sugar-newNF
      }   
      ; applyCM
      
.    
)

***( test 
srew [1]
 aConfig(sig, (fam ('Msg[bound q]) :: msg) 
          (fam ('Enc[bound q]) :: ctxt), cmLeft, insert(fam ('Msg[bound q]), empty),   getOutputs(cmLeft), empty, width 0, length 0)
using 
      STRICT{
        sugar-newNF
      ; applySym1
     } 
.      
)
