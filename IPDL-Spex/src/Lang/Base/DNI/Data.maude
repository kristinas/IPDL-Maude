***( SpeX/Lang/Base/DNI

  This file is part of the SpeX-languages suite.

  Copyright : (C) 2021, 2022 Simion Stoilow Institute of Mathematics
              of the Romanian Academy (IMAR)
  License   : GNU General Public License v2.0 or later (GPL-2.0-or-later)
  Author    : Ionut Tutu <Ionut.Tutu@imar.ro>

)

sload ../Common/Sorts.maude
sload ../Common/Operations.maude
sload ../Common/Predicates.maude
sload ../Common/Nominals.maude
sload ../Common/Typed-modalities.maude
sload ../Common/Sentences.maude

********************************************************************************

fmod DNI/LID is
  sort DNI .
  op DNI : -> DNI [ctor] .
endfm

********************************************************************************

fmod DNI/SPEC-BODY is
  protecting DNI/LID .
  extending  SORT-SET-DECL .
  extending  OP-DECL-SET .
  extending  PRED-DECL-SET .
  extending  NOM-SET-DECL .
  extending  TMOD-DECL-SET .
  extending  SEN-DECL-SET .
  protecting ATTRIBUTE/OPS .

  var  L : DNI .
  vars SB SB' : SpecBody{DNI} .
  var  S : Sort .
  var  SS : SortSet .
  var  SSD : SortSetDecl .
  var  TP : Type .
  var  TPL : TypeList .
  var  OD : OpDecl .
  var  ODS : OpDeclSet .
  var  PD : PredDecl .
  var  PDS : PredDeclSet .
  var  N : Qid .
  var  NS : QidSet .
  var  NSD : NomSetDecl .
  var  MD : TModDecl .
  var  MDS : TModDeclSet .
  var  ED : SenDecl .
  var  EDS : SenDeclSet .

  *** SpecBody{DNI}  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
  sort SpecBody{DNI} .
  op SpecBody[_]{______} : DNI SortSetDecl OpDeclSet PredDeclSet
    NomSetDecl TModDeclSet SenDeclSet -> SpecBody{DNI}
    [ctor format (d d d d d n++i ni ni ni ni ni n--i d)] .

  *** nil (empty DNI specification body)  - - - - - - - - - - - - - - - - - - --
  op nil : -> SpecBody{DNI} .
  eq nil = (SpecBody[DNI]{sorts none . none none noms none . none none}) .

  *** sorts  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
  op sorts : SpecBody{DNI} -> SortSet .
  eq sorts(SpecBody[L]{sorts SS . ODS PDS NSD MDS EDS}) = SS .

  *** add-sort  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - --
  op add-sort : Sort SpecBody{DNI} -> SpecBody{DNI} .
  eq add-sort(S, SpecBody[L]{sorts SS . ODS PDS NSD MDS EDS})
   = SpecBody[L]{sorts (S ; SS) . ODS PDS NSD MDS EDS} .

  *** ops  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
  op ops : SpecBody{DNI} -> OpDeclSet .
  eq ops(SpecBody[L]{SSD ODS PDS NSD MDS EDS}) = ODS .

  *** add-op  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - --
  op add-decl : OpDecl SpecBody{DNI} -> SpecBody{DNI} .
  eq add-decl(OD, SpecBody[L]{SSD ODS PDS NSD MDS EDS})
   = SpecBody[L]{SSD (OD ODS) PDS NSD MDS EDS} .

  *** preds  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
  op preds : SpecBody{DNI} -> PredDeclSet .
  eq preds(SpecBody[L]{SSD ODS PDS NSD MDS EDS}) = PDS .

  *** add-pred  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - --
  op add-decl : PredDecl SpecBody{DNI} -> SpecBody{DNI} .
  eq add-decl(PD, SpecBody[L]{SSD ODS PDS NSD MDS EDS})
   = SpecBody[L]{SSD ODS (PD PDS) NSD MDS EDS} .

  *** noms  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - --
  op noms : SpecBody{DNI} -> QidSet .
  eq noms(SpecBody[L]{SSD ODS PDS noms NS . MDS EDS}) = NS .

  *** add-nom  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
  op add-nom : Qid SpecBody{DNI} -> SpecBody{DNI} .
  eq add-nom(N, SpecBody[L]{SSD ODS PDS noms NS . MDS EDS})
   = SpecBody[L]{SSD ODS PDS noms (N ; NS) . MDS EDS} .

  *** modalities  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - --
  op mods : SpecBody{DNI} -> TModDeclSet .
  eq mods(SpecBody[L]{SSD ODS PDS NSD MDS EDS}) = MDS .

  *** add-mod  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
  op add-decl : TModDecl SpecBody{DNI} -> SpecBody{DNI} .
  eq add-decl(MD, SpecBody[L]{SSD ODS PDS NSD MDS EDS})
   = SpecBody[L]{SSD ODS PDS NSD (MD MDS) EDS} .

  *** symbols* (with a given arity, but other sort)  - - - - - - - - - - - - - -
  op symbols* : SpecBody{DNI} TypeList Type -> QidSet .
  eq symbols*(SpecBody[L]{SSD ODS PDS NSD MDS EDS}, TPL, TP)
   = symbols*(ODS, TPL, TP) ;
     if TP == 'AtomicSentence`{DNI`} then none else symbols(PDS, TPL) fi ;
     if TP == 'Interaction`{DNI`} then none else symbols(MDS, TPL) fi .

  *** constants (of a given sort)  - - - - - - - - - - - - - - - - - - - - - - -
  op consts : SpecBody{DNI} Sort -> QidSet .
  eq consts(SpecBody[L]{SSD ODS PDS noms NS . MDS EDS}, S)
   = consts(ODS, S) ; if S == 'Nominal then NS else none fi .

  *** sens  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - --
  op sens : SpecBody{DNI} -> SenDeclSet .
  eq sens(SpecBody[L]{SSD ODS PDS NSD MDS EDS}) = EDS .

  *** add-sen  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
  op add-sen : SenDecl SpecBody{DNI} -> SpecBody{DNI} .
  eq add-sen(ED, SpecBody[L]{SSD ODS PDS NSD MDS EDS})
   = SpecBody[L]{SSD ODS PDS NSD MDS (ED EDS)} .

  *** sum  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
  op _+_ : SpecBody{DNI} SpecBody{DNI} -> SpecBody{DNI} .
  eq SB + SB'
   = SpecBody[DNI]{
       sorts sorts(SB) ; sorts(SB') .
       (ops(SB) ops(SB'))
       (preds(SB) preds(SB'))
       noms noms(SB) ; noms(SB') .
       (mods(SB) mods(SB'))
       (sens(SB) sens(SB'))
     } .

  *** signature  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
  op signature : SpecBody{DNI} -> SpecBody{DNI} .
  eq signature(SpecBody[L]{SSD ODS PDS NSD MDS EDS})
   = SpecBody[L]{SSD pure(ODS) pure(PDS) NSD MDS none} .
endfm

********************************************************************************
