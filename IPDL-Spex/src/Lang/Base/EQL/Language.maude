***( SpeX/Lang/Base/EQL

  This file is part of the SpeX-languages suite.

  Copyright : (C) 2021, 2022 Simion Stoilow Institute of Mathematics
              of the Romanian Academy (IMAR)
  License   : GNU General Public License v2.0 or later (GPL-2.0-or-later)
  Author    : Ionut Tutu <Ionut.Tutu@imar.ro>

)

sload ../../../Language.maude
sload ../Common/CSList.maude
sload ./Data.maude

********************************************************************************

fmod EQL/LANGUAGE is
  protecting EQL/SPEC-BODY .
  protecting GRAMMAR/CS-LIST .

  var L : EQL .
  var S : Sort .
  var SS : SortSet .
  var ODS : OpDeclSet .
  var EDS : SenDeclSet .

  *** declarations  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - --
  op decl-grammar : EQL -> Grammar .
  eq decl-grammar(L)
   = grammar 'EQL/Syntax is
       'Declaration
         ::= just 'VarDeclaration
         *** sorts
          |  "sort_" : 'Sort
             [format ('! 'so 'd)]
          |  "sorts_" : 'NeSortList
             [format ('! 'so 'd)]
         *** operation symbols
          |  "op_:_->_" : 'Symbol 'Arity 'Sort
             [format ('! 'so 's! 'so 's! 'so 'd)]
          |  "op_:_->_[_]" : 'Symbol 'Arity 'Sort 'NeAttrList
             [format ('! 'so 's! 'so 's! 'so 's! 'o '! 'o)]
          |  "op_:()->_" : 'Symbol 'Sort
             [format ('! 'so 's! 's 'd 's 'so 'd)]
          |  "op_:()->_[_]" : 'Symbol 'Sort 'NeAttrList
             [format ('! 'so 's! 's 'd 's 'so 's! 'o '! 'o)]
          |  "ops_:_->_" : 'NeSymbolList 'Arity 'Sort
             [format ('! 'so 's! 'so 's! 'so 'd)]
          |  "ops_:_->_[_]" : 'NeSymbolList 'Arity 'Sort 'NeAttrList
             [format ('! 'so 's! 'so 's! 'so 's! 'o '! 'o)]
          |  "ops_:()->_" : 'NeSymbolList 'Sort
             [format ('! 'so 's! 's 'd 's 'so 'd)]
          |  "ops_:()->_[_]" : 'NeSymbolList 'Sort 'NeAttrList
             [format ('! 'so 's! 's 'd 's 's 's! 'o '! 'o)]
         *** sentences
          |  "eq_" : 'Sentence
             [format ('! 'so 'd)]
          |  "eq_[_]" : 'Sentence 'NeAttrList
             [format ('! 'so 's! 'o '! 'o)]
       'VarDeclaration
         ::= "var_:_" : 'Symbol 'Sort
             [format ('! 'so 's! 'so 'd)]
          |  "vars_:_" : 'NeSymbolList 'Sort
             [format ('! 'so 's! 'so 'd)]
       'Sort
         ::= token "sort" [exclude ("," "(" ")")]
       'NeSortList
         ::= bubble "neSortList"
       'Symbol
         ::= token "symbol" [exclude ("," ":")]
       'NeSymbolList
         ::= bubble "neSymbolList" [exclude (":")]
       'Arity
         ::= bubble "arity" [exclude ("->" "(" ")" "[" "]")]
       'Sentence
         ::= bubble "sentence" [balanced-parentheses exclude ("label:")]
       'NeAttrList
         ::= bubble "neAttrList" [exclude ("[" "]")]
     endgr .

  *** comma-separated lists of sorts  - - - - - - - - - - - - - - - - - - - - --
  op cs-sorts-grammar : EQL -> Grammar .
  eq cs-sorts-grammar(L)
   = cs-list-grammar('EQL/cs-sorts-syntax, 'NeSortList,
      'Sort ::= token "sort" [exclude ("," "(" ")")]) .

  *** comma-separated lists of symbols  - - - - - - - - - - - - - - - - - - - --
  op cs-symbols-grammar : EQL -> Grammar .
  eq cs-symbols-grammar(L)
   = cs-list-grammar('EQL/cs-symbols-syntax, 'NeSymbolList,
       'Symbol ::= token "symbol" [exclude ("," ":")]) .

  *** operation attributes  - - - - - - - - - - - - - - - - - - - - - - - - - --
  op op-attr-grammar : EQL -> Grammar .
  eq op-attr-grammar(L)
   = grammar 'EQL/op-attr-syntax is
       'Attr ::= lit "assoc" [format ('! 'o)]
              |  lit "comm" [format ('! 'o)]
              |  "id:" : 'Term [format ('! 'o)]
       'AttrList ::= just 'Attr | "__" : 'AttrList 'AttrList [assoc]
       'Term ::= bubble "term" [balanced-parentheses]
     endgr .

  *** sentence attributes  - - - - - - - - - - - - - - - - - - - - - - - - - - -
  op sen-attr-grammar : EQL -> Grammar .
  eq sen-attr-grammar(L)
   = grammar 'EQL/sen-attr-syntax is
       'Attr  ::= "label:_" : 'Label [format ('! 'o 'd)]
       'AttrList ::= just 'Attr | "__" : 'AttrList 'AttrList [assoc]
       'Label ::= token "label"
     endgr .

  *** sentences  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
  op grammar : SpecBody{EQL} -> Grammar .
  eq grammar(SpecBody[L]{sorts SS . ODS EDS})
   = grammar 'EQL/Sentence/Syntax is
       atomic-sentences(L, SS, ODS)
     endgr .

  op atomic-sentences : EQL SortSet OpDeclSet -> GramDeclSet .
  eq atomic-sentences(L, SS, ODS)
   = pr-rules(ODS) eqs(L, SS) .

  op eqs : EQL SortSet -> GramDeclSet .
  eq eqs(L, none) = none .
  eq eqs(L, S ; SS)
   = 'Sentence`{EQL`}
       ::= "_=_" : S S [format ('d 's! 'so 'd) prec (51)]
     eqs(L, SS) .
endfm

*** ----------------------------------------------------------------------------

view EQL from LANGUAGE to EQL/LANGUAGE is
  sort Lid to EQL .
  op Lid to EQL .
  op grammar to decl-grammar .
endv

********************************************************************************

fmod EQL/READ is
  protecting EQL/LANGUAGE .
  protecting ATERM/READ .

  var  L : EQL .
  var  SB : SpecBody{EQL} .
  vars S O V : AConstant .
  vars SL1 SL2 OL1 OL2 A VL1 VL2 E : ATerm .
  vars AtL AtL1 AtL2 T : ATerm .
  var  TL : ATermList .
  var  AtS : AttrSet .

  *** add declaration  - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
  op add-declaration : ATerm SpecBody{EQL} -> SpecBody{EQL} .
  eq add-declaration(T, SB) = SB >>= T .

  op _>>=_ : SpecBody{EQL} ATerm -> SpecBody{EQL} .

  eq SB >>= {'sort_[S] : 'Declaration}
   = add-sort(read-sort(EQL, S), SB) .
  eq SB >>= {'sorts_[S] : 'Declaration}
   = SB >>= {'sort_[S] : 'Declaration} .
  eq SB >>= {'sorts_[{'_`,_[SL1, SL2] : 'NeSortList}] : 'Declaration}
   = SB >>= {'sorts_[SL1] : 'Declaration} >>= {'sorts_[SL2] : 'Declaration} .

  eq SB >>= {'op_:_->_[O, A, S] : 'Declaration}
   = add-decl(read-op(EQL, 'op_:_->_[O, A, S]), SB) .
  eq SB >>= {'op_:_->_`[_`][O, A, S, AtL] : 'Declaration}
   = add-decl(read-op(EQL, 'op_:_->_`[_`][O, A, S, AtL]), SB) .
  eq SB >>= {'op_:`(`)->_[O, S] : 'Declaration}
   = add-decl(read-op(EQL, 'op_:`(`)->_[O, S]), SB) .
  eq SB >>= {'op_:`(`)->_`[_`][O, S, AtL] : 'Declaration}
   = add-decl(read-op(EQL, 'op_:`(`)->_`[_`][O, S, AtL]), SB) .

  eq SB >>= {'ops_:_->_[O, A, S] : 'Declaration}
   = SB >>= {'op_:_->_[O, A, S] : 'Declaration} .
  eq SB >>= {'ops_:_->_[{'_`,_[OL1, OL2] : 'NeSymbolList}, A, S] : 'Declaration}
   = SB >>= {'ops_:_->_[OL1, A, S] : 'Declaration}
        >>= {'ops_:_->_[OL2, A, S] : 'Declaration} .

  eq SB >>= {'ops_:_->_`[_`][O, A, S, AtL] : 'Declaration}
   = SB >>= {'op_:_->_`[_`][O, A, S, AtL] : 'Declaration} .
  eq SB >>= {'ops_:_->_`[_`][
        {'_`,_[OL1, OL2] : 'NeSymbolList}, A, S, AtL] : 'Declaration}
   = SB >>= {'ops_:_->_`[_`][OL1, A, S, AtL] : 'Declaration}
        >>= {'ops_:_->_`[_`][OL2, A, S, AtL] : 'Declaration} .

  eq SB >>= {'ops_:`(`)->_[O, S] : 'Declaration}
   = SB >>= {'op_:`(`)->_[O, S] : 'Declaration} .
  eq SB >>= {'ops_:`(`)->_[{'_`,_[OL1, OL2] : 'NeSymbolList}, S] : 'Declaration}
   = SB >>= {'ops_:`(`)->_[OL1, S] : 'Declaration}
        >>= {'ops_:`(`)->_[OL2, S] : 'Declaration} .

  eq SB >>= {'ops_:`(`)->_`[_`][O, S, AtL] : 'Declaration}
   = SB >>= {'op_:`(`)->_`[_`][O, S, AtL] : 'Declaration} .
  eq SB >>= {'ops_:`(`)->_`[_`][
        {'_`,_[OL1, OL2] : 'NeSymbolList}, S, AtL] : 'Declaration}
   = SB >>= {'ops_:`(`)->_`[_`][OL1, S, AtL] : 'Declaration}
        >>= {'ops_:`(`)->_`[_`][OL2, S, AtL] : 'Declaration} .

  eq SB >>= {'eq_[E] : 'Declaration}
   = add-eq(read-sentence(EQL, E, none), SB) .
  eq SB >>= {'eq_`[_`][E, AtL] : 'Declaration}
   = add-eq(read-sentence(EQL, E, read-attributes(EQL, AtL)), SB) .

  eq SB >>= T = SB [owise] .

  op add-declarations : ATermList SpecBody{EQL} -> SpecBody{EQL} .
  eq add-declarations(empty, SB) = SB .
  eq add-declarations((T, TL), SB)
   = add-declarations(TL, add-declaration(T, SB)) .

  *** read-sort  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
  op read-sort : EQL ATerm ~> Sort .
  eq read-sort(L, S) = read-qid(S) .

  *** read-op  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
  op read-op : EQL ATerm ~> OpDecl .
  eq read-op(L, 'op_:_->_[O, A, S])
   = (op read-symbol(L, O) : read-arity(L, A) -> read-sort(L, S) [none] .) .
  eq read-op(L, 'op_:_->_`[_`][O, A, S, AtL])
   = (op read-symbol(L, O) : read-arity(L, A) -> read-sort(L, S)
      [read-attributes(L, AtL)] .) .
  eq read-op(L, 'op_:`(`)->_[O, S])
   = (op read-symbol(L, O) : nil -> read-sort(L, S) [none] .) .
  eq read-op(L, 'op_:`(`)->_`[_`][O, S, AtL])
   = (op read-symbol(L, O) : nil -> read-sort(L, S)
      [read-attributes(L, AtL)] .) .

  op read-symbol : EQL ATerm ~> Qid .
  eq read-symbol(L, O) = read-qid(O) .

  op read-arity : EQL ATerm ~> TypeList .
  eq read-arity(L, A) = read-qids(A) .

  op read-cs-symbols : EQL ATerm ~> QidSet .
  eq read-cs-symbols(L, {'_`,_[OL1, OL2] : 'NeSymbolList})
   = read-cs-symbols(L, OL1) ; read-cs-symbols(L, OL2) .
  eq read-cs-symbols(L, {O : 'Symbol})
   = read-symbol(L, O) .

  *** read-variables  - - - - - - - - - - - - - - - - - - - - - - - - - - - - --
  sort ReadVarOptions{EQL} .
  op all`vars : -> ReadVarOptions{EQL} [ctor] .
  op protecting`constants : -> ReadVarOptions{EQL} [ctor] .

  var RVO : ReadVarOptions{EQL} .

  op read-vars : SpecBody{EQL} ATerm ReadVarOptions{EQL} -> VariableSet .
  eq read-vars(SB, {'var_:_[V, S] : 'VarDeclaration}, RVO)
   = read-var(SB, 'var_:_[V, S], RVO) .
  eq read-vars(SB, {'vars_:_[V, S] : 'VarDeclaration}, RVO)
   = read-var(SB, 'var_:_[V, S], RVO) .
  eq read-vars(SB, {'vars_:_[
          {'_`,_[VL1, VL2] : 'NeSymbolList}, S] : 'VarDeclaration}, RVO)
   = read-vars(SB, {'vars_:_[VL1, S] : 'VarDeclaration}, RVO) ;
     read-vars(SB, {'vars_:_[VL2, S] : 'VarDeclaration}, RVO) .
  eq read-vars(SB, T, RVO) = none [owise] .

  op read-var : SpecBody{EQL} ATerm ReadVarOptions{EQL} -> VariableSet .
  eq read-var(SB, 'var_:_[V, S], RVO)
   = if (RVO == protecting constants) and-then (name(V) in consts(SB, type(V)))
     then none
    else var(read-symbol(EQL, V), read-sort(EQL, S)) fi .
  eq read-var(SB, T, RVO) = none [owise] .

  *** read-sentence  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
  op read-sentence : EQL ATerm AttrSet -> SenDecl .
  eq read-sentence(L, E, AtS) = (sen E [AtS] .) .

  *** read-attributes  - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
  op read-attributes : EQL ATerm -> AttrSet .
  eq read-attributes(L, {'__[AtL1, AtL2] : 'AttrList})
   = read-attributes(L, AtL1) read-attributes(L, AtL2) .
  eq read-attributes(L, {'assoc.Attr : 'Attr}) = assoc .
  eq read-attributes(L, {'comm.Attr : 'Attr}) = comm .
  eq read-attributes(L, {'id:[T] : 'Attr}) = id(plain-term(T)) .
  eq read-attributes(L, {'label:_[T] : 'Attr}) = label(read-qid(T)) .
  eq read-attributes(L, T) = none [owise] .
endfm

********************************************************************************

fmod EQL/SHOW is
  protecting PRINTING { EQL } .

  var  L : EQL .
  var  G : Grammar .
  var  SB : SpecBody{EQL} .
  var  S : Sort .
  var  SS : SortSet .
  var  SSD : SortSetDecl .
  var  NSL : NeQidList .
  vars O QI : Qid .
  var  A : NeTypeList .
  var  At : Attr .
  var  AtS : AttrSet .
  var  AtL : AttrList .
  var  OD : OpDecl .
  var  ODS : OpDeclSet .
  var  ODL : OpDeclList .
  var  E : ATerm .
  vars T : Term .
  var  NTL : NeTermList .
  var  ED : SenDecl .
  var  EDS : SenDeclSet .
  var  EDL : SenDeclList .

  *** show body  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
  op show : SpecBody{EQL} -> TermList .
  eq show(SB) = show-fragment(SB, SB) .

  op show-fragment : SpecBody{EQL} SpecBody{EQL} -> TermList .
  eq show-fragment(SpecBody[L]{SSD ODS EDS}, SB)
   = show-sorts(L, list(SSD)),
     show-ops(L, SB, topo-sort(ODS)),
     show-sens(L, SB, list(EDS)) .

  *** show sorts  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - --
  op show-sorts : EQL QidList -> TermList .
  eq show-sorts(L, nil) = empty .
  eq show-sorts(L, S) = 'sort_[show-sort(L, S)] .
  eq show-sorts(L, S NSL) = 'sorts_[cs-bubble('neSortList, show-qids(S NSL))] .

  op show-sort : EQL Qid -> Term .
  eq show-sort(L, S) = mk-bubble('sort, show-qid(S)) .

  *** show ops  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - --
  op show-ops : EQL SpecBody{EQL} OpDeclList -> TermList .
  eq show-ops(L, SB, nil) = empty .
  eq show-ops(L, SB, {op O : A -> S [AtS] .} ODL)
   = if AtS == none
     then 'op_:_->_[ show-symbol(L, O), show-arity(L, A), show-sort(L, S) ]
     else 'op_:_->_`[_`] [
        show-symbol(L, O), show-arity(L, A), show-sort(L, S),
        show-op-attributes(L, SB, AtS) ] fi,
     show-ops(L, SB, ODL) .
  eq show-ops(L, SB, {op O : nil -> S [AtS] .} ODL)
   = 'op_:`(`)->_[ show-symbol(L, O), show-sort(L, S) ], show-ops(L, SB, ODL) .

  op show-symbol : EQL Qid -> Term .
  eq show-symbol(L, O) = mk-bubble('symbol, show-qid(O)) .

  op show-arity : EQL NeTypeList -> TermList .
  eq show-arity(L, nil) = empty .
  eq show-arity(L, A) = mk-bubble('arity, show-qids(A)) .

  op show-op-attributes : EQL SpecBody{EQL} AttrSet -> TermList .
  eq show-op-attributes(L, SB, AtS)
   = show-attributes(L, op-attr-grammar(L), (show-assoc-attr(L, AtS),
        show-comm-attr(L, AtS), show-id-attr(SB, AtS))) .

  *** show sentences  - - - - - - - - - - - - - - - - - - - - - - - - - - - - --
  op show-sens : EQL SpecBody{EQL} SenDeclList -> TermList .
  eq show-sens(L, SB, nil) = empty .
  eq show-sens(L, SB, {sen E [none] .} EDL)
   = 'eq_ [ show-sentence(SB, E) ], show-sens(L, SB, EDL) .
  eq show-sens(L, SB, {sen E [AtS] .} EDL)
   = 'eq_`[_`] [ show-sentence(SB, E), show-sen-attributes(L, AtS) ],
     show-sens(L, SB, EDL) [owise] .

  op show-sentence : SpecBody{EQL} ATerm -> Term .
  eq show-sentence(SB, E)
   = mk-bubble('sentence, show-qids(print term E given grammar(SB))) .

  op show-term : SpecBody{EQL} Term -> Term .
  eq show-term(SB, T)
   = mk-bubble('term, show-qids(print term T given grammar(SB))) .

  op show-sen-attributes : EQL AttrSet -> TermList .
  eq show-sen-attributes(L, AtS)
   = show-attributes(L, sen-attr-grammar(L), show-label-attr(L, list(AtS))) .

  *** show attributes  - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
  op show-attributes : EQL Grammar NeTermList -> Term .
  eq show-attributes(L, G, NTL)
   = mk-bubble('neAttrList, show-qids(print term pack('__, NTL) given G)) .

  op show-assoc-attr : EQL AttrSet -> TermList .
  eq show-assoc-attr(L, assoc AtS) = 'assoc.Attr .
  eq show-assoc-attr(L, AtS) = empty [owise] .

  op show-comm-attr : EQL AttrSet -> TermList .
  eq show-comm-attr(L, comm AtS) = 'comm.Attr .
  eq show-comm-attr(L, AtS) = empty [owise] .

  op show-id-attr : SpecBody{EQL} AttrSet -> TermList .
  ceq show-id-attr(SB, id(T) AtS) = 'id:[show-term(SB, T)] if AtS is id-free .
  eq show-id-attr(SB, AtS) = empty [owise] .

  op show-label-attr : EQL AttrList -> TermList .
  eq show-label-attr(L, nil) = empty .
  eq show-label-attr(L, {label(QI)} AtL)
   = 'label:_[mk-bubble('label, show-qid(QI))], show-label-attr(L, AtL) .
  eq show-label-attr(L, {At} AtL) = show-label-attr(L, AtL) [owise] .
endfm

********************************************************************************

fmod EQL/ANALYSIS-STATE is
  protecting EQL/READ .

  var  L : EQL .
  var  SB : SpecBody{EQL} .
  var  VS : VariableSet .
  var  AtS : AttrSet .
  var  TP : Type .
  var  TPL : TypeList .
  var  V : AConstant .
  vars T VL1 VL2 : ATerm .
  var  RVO : ReadVarOptions{EQL} .

  *** analysis state  - - - - - - - - - - - - - - - - - - - - - - - - - - - - --
  sort AnalysisState{EQL} .
  op AnalysisState[_]{_&_&_} : EQL SpecBody{EQL} VariableSet AttrSet
    -> AnalysisState{EQL} [ctor] .

  *** initial-state (of a specification body)  - - - - - - - - - - - - - - - - -
  op initial-state : SpecBody{EQL} -> AnalysisState{EQL} .
  eq initial-state(SB) = AnalysisState[EQL]{SB & none & none} .

  *** projections  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
  op spec-body : AnalysisState{EQL} -> SpecBody{EQL} .
  eq spec-body(AnalysisState[L]{SB & VS & AtS}) = SB .

  op vars : AnalysisState{EQL} -> VariableSet .
  eq vars(AnalysisState[L]{SB & VS & AtS}) = VS .

  op attrs : AnalysisState{EQL} -> AttrSet .
  eq attrs(AnalysisState[L]{SB & VS & AtS}) = AtS .

  *** state updates  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
  op _>>=_ : AnalysisState{EQL} ATerm -> AnalysisState{EQL} .

  ceq AnalysisState[L]{SB & VS & AtS} >>= T
   = AnalysisState[L]{SB & VS ; read-vars(SB, T, protecting constants) & AtS}
   if T :: 'VarDeclaration .

  ceq AnalysisState[L]{SB & VS & AtS} >>= T
   = AnalysisState[L]{SB & VS & AtS read-attributes(EQL, T)}
   if T :: ('Attr ; 'AttrList) .

  eq AnalysisState[L]{SB & VS & AtS} >>= T
   = AnalysisState[L]{SB >>= T & VS & AtS} [owise] .

  *** auxiliary operations on the underlying spec-body of a state  - - - - - - -
  op sorts : AnalysisState{EQL} -> SortSet .
  eq sorts(AnalysisState[L]{SB & VS & AtS}) = sorts(SB) .

  op ops : AnalysisState{EQL} -> OpDeclSet .
  eq ops(AnalysisState[L]{SB & VS & AtS}) = ops(SB) .

  op symbols* : AnalysisState{EQL} TypeList Type -> QidSet .
  eq symbols*(AnalysisState[L]{SB & VS & AtS}, TPL, TP)
   = symbols*(SB, TPL, TP) .

  op grammar : AnalysisState{EQL} -> Grammar .
  eq grammar(AnalysisState[L]{SB & VS & AtS}) = grammar(SB) .

  *** read-variables  - - - - - - - - - - - - - - - - - - - - - - - - - - - - --
  op read-vars : AnalysisState{EQL} ATerm ReadVarOptions{EQL} -> VariableSet .
  eq read-vars(AnalysisState[L]{SB & VS & AtS}, T, RVO)
   = read-vars(SB, T, RVO) .
endfm

*** ----------------------------------------------------------------------------

view EQL/AnalysisState from ANALYSIS-STATE to EQL/ANALYSIS-STATE is
  sort State to AnalysisState{EQL} .
endv

*** ----------------------------------------------------------------------------

fmod EQL/PARSING is
  including  SYNTACTIC-ANALYSIS { EQL, EQL/AnalysisState } .
  protecting LANG-NAME { EQL } .
  protecting EQL/SHOW .

  var  L : EQL .
  vars SB SB' Sig Sig' : SpecBody{EQL} .
  var  SAS : AnalysisState{EQL} .
  var  QIL : QidList .
  var  QIS : QidSet .
  vars S O V : AConstant .
  vars $S $SL SL SL1 SL2 : ATerm .
  vars $O $OL OL OL1 OL2 $A A : ATerm .
  vars $V $VL VL VL1 VL2 : ATerm .
  vars $T T $E E : ATerm .
  vars $AtL AtL AtL1 AtL2 : ATerm .
  var  TL : ATermList .
  vars $ QI : Qid .
  var  CA : Constant .
  var  TPL : TypeList .
  var  TP : Type .
  vars #O #A : Nat .
  var  AO : AnalysisOutcome .

  *** parse input  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
  eq parse L input (QIL) in SAS
   = scan L input (QIL : 'Declaration)
     then analyse L declaration in SAS
     or-else, if inconclusive, parsing-error ('unexpected 'parsing 'error:
      'cannot 'parse 'this name(L) 'declaration) .

  *** analyse declaration  - - - - - - - - - - - - - - - - - - - - - - - - - - -
  op analyse_declaration : EQL -> AnalysisAction{EQL} .

  eq analyse L declaration {'sort_[$S] : 'Declaration}
   = try {'sort_ [ solve L sort ($S) ] : 'Declaration} .

  eq analyse L declaration {'sorts_[$SL] : 'Declaration}
   = try {'sorts_ [ solve L sorts ($SL) ] : 'Declaration} .

  eq analyse L declaration {'op_:_->_[$O, $A, $S] : 'Declaration}
   = try { 'op_:_->_ [
        solve L op-symbol ($O),
        solve L arity ($A) then check that all sorts in L arity are declared,
        solve L sort ($S) then check that L sort is declared
      ] : 'Declaration }
     then check validity constraints for L op .

  eq analyse L declaration {'op_:_->_`[_`][$O, $A, $S, $AtL] : 'Declaration}
   = try { 'op_:_->_`[_`] [
        solve L op-symbol ($O),
        solve L arity ($A) then check that all sorts in L arity are declared,
        solve L sort ($S) then check that L sort is declared,
        solve L op-attributes ($AtL)
      ] : 'Declaration }
     then check validity constraints for L op .

  eq analyse L declaration {'op_:`(`)->_[$O, $S] : 'Declaration}
   = try { 'op_:`(`)->_ [
        solve L op-symbol ($O),
        solve L sort ($S) then check that L sort is declared
      ] : 'Declaration }
     then check validity constraints for L op .

  eq analyse L declaration {'op_:`(`)->_`[_`][$O, $S, $AtL] : 'Declaration}
   = try { 'op_:`(`)->_`[_`] [
        solve L op-symbol ($O),
        solve L sort ($S) then check that L sort is declared,
        solve L op-attributes ($AtL)
      ] : 'Declaration }
     then check validity constraints for L op .

  eq analyse L declaration {'ops_:_->_[$OL, $A, $S] : 'Declaration}
   = try { 'ops_:_->_ [
        solve L op-symbols ($OL),
        solve L arity ($A) then check that all sorts in L arity are declared,
        solve L sort ($S) then check that L sort is declared
      ] : 'Declaration }
     then check validity constraints for L ops .

  eq analyse L declaration {'ops_:_->_`[_`][$OL, $A, $S, $AtL] : 'Declaration}
   = try { 'ops_:_->_`[_`] [
        solve L op-symbols ($OL),
        solve L arity ($A) then check that all sorts in L arity are declared,
        solve L sort ($S) then check that L sort is declared,
        solve L op-attributes ($AtL)
      ] : 'Declaration }
     then check validity constraints for L ops .

  eq analyse L declaration {'ops_:`(`)->_[$OL, $S] : 'Declaration}
   = try { 'ops_:`(`)->_ [
        solve L op-symbols ($OL),
        solve L sort ($S) then check that L sort is declared
      ] : 'Declaration }
     then check validity constraints for L ops .

  eq analyse L declaration {'ops_:`(`)->_`[_`][$OL, $S, $AtL] : 'Declaration}
   = try { 'ops_:`(`)->_`[_`] [
        solve L op-symbols ($OL),
        solve L sort ($S) then check that L sort is declared,
        solve L op-attributes ($AtL)
      ] : 'Declaration }
     then check validity constraints for L ops .

  eq analyse L declaration {'var_:_[$V, $S] : 'VarDeclaration}
   = try { 'var_:_ [
        solve L variable ($V),
        solve L sort ($S) then check that L sort is declared
      ] : 'VarDeclaration } .

  eq analyse L declaration {'vars_:_[$VL, $S] : 'VarDeclaration}
   = try { 'vars_:_ [
        solve L variables ($VL),
        solve L sort ($S) then check that L sort is declared
      ] : 'VarDeclaration } .

  eq analyse L declaration {'eq_[$E] : 'Declaration}
   = try { 'eq_ [
        analyse L sentence ($E)
      ] : 'Declaration } .

  eq analyse L declaration {'eq_`[_`][$E, $AtL] : 'Declaration}
   = try { 'eq_`[_`] [
        analyse L sentence ($E),
        solve L sen-attributes ($AtL)
      ] : 'Declaration } .

  *** analyse individual sorts  - - - - - - - - - - - - - - - - - - - - - - - -
  op solve_sort : EQL -> AnalysisAction{EQL} .
  ceq solve L sort {$[S] : 'Sort}
   = check (QI :: Sort and QI is ordinary) and-then return {S : 'Sort}
     or-else
     fail ('syntax 'error: spaced-emph(print L term ($[S]))
      'is 'not 'a 'valid 'sort, (at L term (S), include context))
   if QI := read-qid(S) .

  *** analyse comma-separated lists of sorts  - - - - - - - - - - - - - - - - -
  op solve_sorts : EQL -> AnalysisAction{EQL} .
  eq solve L sorts {$[SL] : 'NeSortList}
   = decode input (read-qids(SL) : 'NeSortList) given cs-sorts-grammar(L)
     then solve L sorts .
  eq solve L sorts {'_`,_[SL1, SL2] : 'NeSortList}
   = try {'_`,_ [ solve L sorts (SL1), solve L sorts (SL2) ] : 'NeSortList} .
  eq solve L sorts {$[S] : 'Sort}
   = solve L sort {$[S] : 'Sort} .

  *** analyse individual operations  - - - - - - - - - - - - - - - - - - - - - -
  op solve_op-symbol : EQL -> AnalysisAction{EQL} .
  eq solve L op-symbol {$[O] : 'Symbol}
   = check (read-qid(O) is ordinary) and-then return {O : 'Symbol}
     or-else
     fail ('syntax 'error: spaced-emph(print L term ($[O]))
      'is 'not 'a 'valid 'operation 'symbol,
      (at L term (O), include context)) .

  op solve_arity : EQL -> AnalysisAction{EQL} .
  ceq solve L arity {$[A] : 'Arity}
   = check (QIL :: NeTypeList) and-then return {A : 'Arity}
     or-else
     fail ('syntax 'error: spaced-emph(print L term ($[A]))
      'is 'not 'a 'valid 'arity,
      (at L term (A), include context))
   if QIL := read-qids(A) .

  op check`that`all`sorts`in_arity`are`declared : EQL -> AnalysisAction{EQL} .
  eq check that all sorts in L arity are declared {A : 'Arity}
   = try {check that all sorts in L sort-seq are declared (A) : 'Arity} .

  op check`that`all`sorts`in_sort-seq`are`declared :
      EQL -> AnalysisAction{EQL} .
  eq check that all sorts in L sort-seq are declared {'__[TL] : 'NeQidList}
   = try { '__ [
        check that all sorts in L sort-seq are declared (TL)
      ] : 'NeQidList} .
  eq check that all sorts in L sort-seq are declared (empty)
   = return (empty) .
  eq check that all sorts in L sort-seq are declared ((S, TL))
   = check that L sort is declared (S)
     and-then check that all sorts in L sort-seq are declared (TL) .

  op check`that_sort`is`declared : EQL -> AnalysisAction{EQL} .
  eq check that L sort is declared (S) in SAS
   = check (read-sort(L, S) in sorts(SAS)) and-then return (S)
     or-else
     fail ('undeclared 'sort emph(read-sort(L, S)),
      (at L term (S), include context))
     in SAS .

  op solve_op-attributes : EQL -> AnalysisAction{EQL} .
  eq solve L op-attributes {$[AtL] : 'NeAttrList} in SAS
   = decode input (read-qids(AtL) : 'AttrList) given op-attr-grammar(L) .

  op check`validity`constraints`for_op : EQL -> AnalysisAction{EQL} .
  eq check validity constraints for L op
       {'op_:_->_[O, A, S] : 'Declaration}
   = try { 'op_:_->_ [
        check validity constraints for L op
          [read-arity(L, A) -> read-sort(L, S)] (O),
        return (A), return (S)
      ] : 'Declaration } .
  ceq check validity constraints for L op
       {'op_:_->_`[_`][O, A, S, AtL] : 'Declaration}
   = try { 'op_:_->_`[_`] [
        check validity constraints for L op [TPL -> TP] (O),
        return (A), return (S),
        analyse L op-attributes [QI : TPL -> TP] (AtL)
        ] : 'Declaration }
   if QI := read-symbol(L, O)
      /\ TPL := read-arity(L, A) /\ TP := read-sort(L, S) .
  eq check validity constraints for L op
       {'op_:`(`)->_[O, S] : 'Declaration}
   = try { 'op_:`(`)->_ [
        check validity constraints for L op [nil -> read-sort(L, S)] (O),
        return (S)
      ] : 'Declaration } .
  ceq check validity constraints for L op
       {'op_:`(`)->_`[_`][O, S, AtL] : 'Declaration}
   = try { 'op_:`(`)->_`[_`] [
        check validity constraints for L op [nil -> TP] (O),
        return (S),
        analyse L op-attributes [QI : nil -> TP] (AtL)
      ] : 'Declaration }
   if QI := read-symbol(L, O) /\ TP := read-sort(L, S) .

  op check`validity`constraints`for_op`[_->_`] :
      EQL [TypeList] [Type] -> AnalysisAction{EQL} .
  eq check validity constraints for L op [nil -> TP]
   = check underscores of L op [nil] .
  eq check validity constraints for L op [TPL -> TP]
   = check underscores of L op [TPL] ;
     check overloading of L symbol [TPL -> TP] [owise] .

  op check`underscores`of_op`[_`] : EQL [TypeList] -> AnalysisAction{EQL} .
  ceq check underscores of L op [TPL] (O)
   = check (#O == 0 or #O == #A) and-then return (O)
     or-else
     fail ('number 'of 'underscores '\s '`( qid(#O) '`)
      'does 'not 'match 'number 'of 'arguments '\s '`( qid(#A) '`)
      'for 'operation emph(read-symbol(L, O)),
      (at L term (O), include context))
   if #O := #underscores(read-symbol(L, O)) /\ #A := size(TPL) .

  op check`overloading`of_symbol`[_->_`] :
      EQL [TypeList] [Type] -> AnalysisAction{EQL} .
  eq check overloading of L symbol [TPL -> TP] (O) in SAS
   = check (read-symbol(L, O) not-in symbols*(SAS, TPL, TP)) and-then return (O)
     or-else
     fail ('cannot 'overload 'symbol emph(read-symbol(L, O))
      'with 'arity emph(TPL), (at L term (O), include context))
     in SAS .

  op analyse_op-attributes`[_:_->_`] :
      EQL [QidSet] [TypeList] [Type] -> AnalysisAction{EQL} .
  eq analyse L op-attributes [QIS : TPL -> TP] {'__[AtL1, AtL2] : 'AttrList}
   = try { '__ [
        analyse L op-attributes [QIS : TPL -> TP] (AtL1),
        analyse L op-attributes [QIS : TPL -> TP] (AtL2)
      ] : 'AttrList } .
  eq analyse L op-attributes [QIS : TPL -> TP] {CA : 'Attr} in SAS
   = (check (read-attributes(EQL, {CA : 'Attr}) is equational
        implies TPL == TP TP)
      or-else
      fail (emph(print term CA given op-attr-grammar(L))
        'attribute 'can 'be 'declared 'only 'for 'binary 'operation
        'symbols 'whose 'domain 'sorts 'and 'range 'sort 'are 'the 'same,
        (at term CA given op-attr-grammar(L), include context)))
     and-then return {CA : 'Attr} in SAS .
  eq analyse L op-attributes [QIS : TPL -> TP] {'id:[$T] : 'Attr} in SAS
   = (check (TPL == TP TP)
      or-else
      fail (emph('id) 'attribute
        'can 'be 'declared 'only 'for 'binary 'operation 'symbols
        'whose 'domain 'sorts 'and 'range 'sort 'are 'the 'same,
        (at term 'id:[$T] given op-attr-grammar(L), include context)))
     and-then
     (check (attrs(SAS) is id-free)
      or-else
      fail ('multiple 'identity 'declarations,
        (at term 'id:[$T] given op-attr-grammar(L), include context)))
     and-then
     try {'id: [ solve L term [TP] ($T) ] : 'Attr} in SAS .

  op solve_term`[_`] : EQL [Type] -> AnalysisAction{EQL} .
  eq solve L term [TP] {$[T] : 'Term} in SAS
   = decode input (read-qids(T) : TP) given (grammar(SAS) & vars(SAS)) .

  *** analyse comma-separated lists of operation symbols  - - - - - - - - - - --
  op solve_op-symbols : EQL -> AnalysisAction{EQL} .
  eq solve L op-symbols {$[OL] : 'NeSymbolList}
   = decode input (read-qids(OL) : 'NeSymbolList) given cs-symbols-grammar(L)
     then solve L op-symbols .
  eq solve L op-symbols {'_`,_[OL1, OL2] : 'NeSymbolList}
   = try { '_`,_ [
        solve L op-symbols (OL1),
        solve L op-symbols (OL2)
      ] : 'NeSymbolList } .
  eq solve L op-symbols {$[O] : 'Symbol}
   = solve L op-symbol {$[O] : 'Symbol} .

  op check`validity`constraints`for_ops : EQL -> AnalysisAction{EQL} .
  eq check validity constraints for L ops
      {'ops_:_->_[OL, A, S] : 'Declaration}
   = try { 'ops_:_->_ [
        check validity constraints for L ops
          [read-arity(L, A) -> read-sort(L, S)] (OL),
        return (A), return (S)
      ] : 'Declaration } .
  ceq check validity constraints for L ops
      {'ops_:_->_`[_`][OL, A, S, AtL] : 'Declaration}
   = try { 'ops_:_->_`[_`] [
        check validity constraints for L ops [TPL -> TP] (OL),
        return (A), return (S),
        analyse L op-attributes [QIS : TPL -> TP] (AtL)
      ] : 'Declaration }
   if QIS := read-cs-symbols(L, OL)
      /\ TPL := read-arity(L, A) /\ TP := read-sort(L, S) .
  eq check validity constraints for L ops
      {'ops_:`(`)->_[OL, S] : 'Declaration}
   = try { 'ops_:`(`)->_ [
        check validity constraints for L ops [nil -> read-sort(L, S)] (OL),
        return (S)
      ] : 'Declaration } .
  ceq check validity constraints for L ops
      {'ops_:`(`)->_`[_`][OL, S, AtL] : 'Declaration}
   = try { 'ops_:`(`)->_`[_`] [
        check validity constraints for L ops [nil -> TP] (OL),
        return (S),
        analyse L op-attributes [QIS : nil -> TP] (AtL)
      ] : 'Declaration }
   if QIS := read-cs-symbols(L, OL) /\ TP := read-sort(L, S) .

  op check`validity`constraints`for_ops`[_->_`] :
      EQL [TypeList] [Type] -> AnalysisAction{EQL} .
  eq check validity constraints for L ops [TPL -> TP]
      {'_`,_[OL1, OL2] : 'NeSymbolList}
   = try { '_`,_ [
        check validity constraints for L ops [TPL -> TP] (OL1),
        check validity constraints for L ops [TPL -> TP] (OL2)
      ] : 'NeSymbolList } .
  eq check validity constraints for L ops [TPL -> TP] {O : 'Symbol}
   = check validity constraints for L op [TPL -> TP] {O : 'Symbol} .

  *** analyse individual variables  - - - - - - - - - - - - - - - - - - - - - --
  op solve_variable : EQL -> AnalysisAction{EQL} .
  eq solve L variable {$[V] : 'Symbol}
   = check (read-qid(V) is ordinary)
     and-then return {V : 'Symbol}
     or-else
     fail ('syntax 'error: spaced-emph(print L term ($[V]))
      'is 'not 'a 'valid 'variable,
      (at L term (V), include context)) .

  *** analyse comma-separated lists of variables  - - - - - - - - - - - - - - --
  op solve_variables : EQL -> AnalysisAction{EQL} .
  eq solve L variables {$[VL] : 'NeSymbolList}
   = decode input (read-qids(VL) : 'NeSymbolList) given cs-symbols-grammar(L)
     then solve L variables .
  eq solve L variables {'_`,_[VL1, VL2] : 'NeSymbolList}
   = try { '_`,_ [
        solve L variables (VL1),
        solve L variables (VL2)
      ] : 'NeSymbolList } .
  eq solve L variables {$[V] : 'Symbol}
   = solve L variable {$[V] : 'Symbol} .

  *** analyse sentences  - - - - - - - - - - - - - - - - - - - - - - - - - - - -
  op analyse_sentence : EQL -> AnalysisAction{EQL} .
  eq analyse L sentence {$[E] : 'Sentence} in SAS
   = decode input (read-qids(E) : 'Sentence`{EQL`})
     given (grammar(SAS) & vars(SAS)) .

  op solve_sen-attributes : EQL -> AnalysisAction{EQL} .
  eq solve L sen-attributes {$[AtL] : 'NeAttrList}
   = decode input (read-qids(AtL) : 'AttrList) given sen-attr-grammar(L)
     then solve L sen-attributes .
  eq solve L sen-attributes {'__[AtL1, AtL2] : 'AttrList}
   = try { '__ [
        solve L sen-attributes (AtL1),
        solve L sen-attributes (AtL2)
      ] : 'AttrList} .
  eq solve L sen-attributes {'label:_[$T] : 'Attr}
   = try {'label:_ [ solve L label ($T) ] : 'Attr} .

  op solve_label : EQL -> AnalysisAction{EQL} .
  eq solve L label {$[T] : 'Label}
   = check (read-qid(T) :: Qid) and-then return {T : 'Label}
     or-else
     fail ('syntax 'error: spaced-emph(print L term ($[T])) 'is 'not 'a 'valid
      'label, (at L term (T), include context)) .

  *** reanalyse  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
  op reanalyse : SpecBody{EQL} AnalysisState{EQL} -> AnalysisOutcome .
  ceq reanalyse(SB, SAS)
   = if AO :: SuccessfulAnalysis
     then reanalyse(EQL, show-fragment(SB, SB' + SB), SAS)
     else AO fi
   if SB' := spec-body(SAS)
      /\ Sig := signature(SB)
      /\ Sig' := signature(SB')
      /\ AO := reanalyse(EQL, show-fragment(Sig, Sig' + Sig), SAS) .
endfm

********************************************************************************

fmod EQL/PRINTING is
  protecting EQL/SHOW .

  var SB : SpecBody{EQL} .

  *** print  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
  op print : SpecBody{EQL} -> QidList .
  eq print(SB) = intercalate('\n, print EQL terms show(SB)) .
endfm

********************************************************************************

fmod EQL/PARSING-&-PRINTING is
  protecting EQL/PARSING .
  protecting EQL/PRINTING .
endfm

********************************************************************************
