***( SpeX/Lang/Base/MSA

  This file is part of the SpeX-languages suite.

  Copyright : (C) 2021, 2022 Simion Stoilow Institute of Mathematics
              of the Romanian Academy (IMAR)
  License   : GNU General Public License v2.0 or later (GPL-2.0-or-later)
  Author    : Ionut Tutu <Ionut.Tutu@imar.ro>

)

sload ../Common/Sorts.maude
sload ../Common/Operations.maude
sload ../Common/Sentences.maude

********************************************************************************

fmod MSA/LID is
  sort MSA .
  op MSA : -> MSA [ctor] .
endfm

********************************************************************************

fmod MSA/SPEC-BODY is
  protecting MSA/LID .
  extending  SORT-SET-DECL .
  extending  OP-DECL-SET .
  extending  SEN-DECL-SET .
  protecting ATTRIBUTE/OPS .

  var  L : MSA .
  vars SB SB' : SpecBody{MSA} .
  var  S : Sort .
  var  SS : SortSet .
  var  SSD : SortSetDecl .
  var  TP : Type .
  var  TPL : TypeList .
  var  OD : OpDecl .
  var  ODS : OpDeclSet .
  var  ED : SenDecl .
  var  EDS : SenDeclSet .

  *** SpecBody{MSA}  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
  sort SpecBody{MSA} .
  op SpecBody[_]{___} : MSA SortSetDecl OpDeclSet SenDeclSet -> SpecBody{MSA}
    [ctor format (d d d d d n++i ni ni n--i d)] .

  *** nil (empty MSA specification body)  - - - - - - - - - - - - - - - - - - --
  op nil : -> SpecBody{MSA} .
  eq nil = (SpecBody[MSA]{sorts none . none none}) .

  *** sorts  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
  op sorts : SpecBody{MSA} -> SortSet .
  eq sorts(SpecBody[L]{sorts SS . ODS EDS}) = SS .

  *** add-sort  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - --
  op add-sort : Sort SpecBody{MSA} -> SpecBody{MSA} .
  eq add-sort(S, SpecBody[L]{sorts SS . ODS EDS})
   = SpecBody[L]{sorts (S ; SS) . ODS EDS} .

  *** ops  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
  op ops : SpecBody{MSA} -> OpDeclSet .
  eq ops(SpecBody[L]{SSD ODS EDS}) = ODS .

  *** add-op  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - --
  op add-decl : OpDecl SpecBody{MSA} -> SpecBody{MSA} .
  eq add-decl(OD, SpecBody[L]{SSD ODS EDS}) = SpecBody[L]{SSD (OD ODS) EDS} .

  *** symbols* (with a given arity, but other sort)  - - - - - - - - - - - - - -
  op symbols* : SpecBody{MSA} TypeList Type -> QidSet .
  eq symbols*(SpecBody[L]{SSD ODS EDS}, TPL, TP) = symbols*(ODS, TPL, TP) .

  *** constants (of a given sort)  - - - - - - - - - - - - - - - - - - - - - - -
  op consts : SpecBody{MSA} Sort -> QidSet .
  eq consts(SpecBody[L]{SSD ODS EDS}, S) = consts(ODS, S) .

  *** sens  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - --
  op sens : SpecBody{MSA} -> SenDeclSet .
  eq sens(SpecBody[L]{SSD ODS EDS}) = EDS .

  *** add-sen  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
  op add-sen : SenDecl SpecBody{MSA} -> SpecBody{MSA} .
  eq add-sen(ED, SpecBody[L]{SSD ODS EDS}) = SpecBody[L]{SSD ODS (ED EDS)} .

  *** add  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
  op _+_ : SpecBody{MSA} SpecBody{MSA} -> SpecBody{MSA} .
  eq SB + SB'
   = SpecBody[MSA]{
       sorts sorts(SB) ; sorts(SB') .
       (ops(SB) ops(SB'))
       (sens(SB) sens(SB'))
     } .

  *** signature  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
  op signature : SpecBody{MSA} -> SpecBody{MSA} .
  eq signature(SpecBody[L]{SSD ODS EDS}) = SpecBody[L]{SSD pure(ODS) none} .
endfm

********************************************************************************
